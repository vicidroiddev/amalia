apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.github.dcendents.android-maven'

group = "com.github.vicidroiddev"

buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

android {
    compileSdkVersion toolchainVersions.compileSdk
    buildToolsVersion toolchainVersions.buildTools

    defaultConfig {
        minSdkVersion toolchainVersions.minSdk
        targetSdkVersion toolchainVersions.targetSdk
        versionCode 1
        versionName "1.0 (alpha)"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    // Target Java 8 to use androidx.lifecycle.DefaultLifecycleObserver
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

androidExtensions {
    //PARCELIZE SUPPORT
    experimental = true
}

dependencies {
    implementation deps.kotlinStdlib

    testImplementation deps.junit
    testImplementation deps.mjaarmanMockito
    testImplementation deps.roboelectric
    testImplementation archDeps.archCoreTesting

    implementation archDeps.extensions
    implementation archDeps.commonJava8

    implementation androidXDeps.appcompat
    implementation androidXDeps.annotation
    implementation androidXDeps.recyclerview
    implementation androidXDeps.savedState
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}

apply plugin: 'kotlin-android-extensions'

