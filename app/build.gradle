apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

Properties devProperties = new Properties()
def devPropertiesFile = project.file('dev.properties')

if (devPropertiesFile.exists()) {
    devProperties.load(devPropertiesFile.newDataInputStream())
}

def devPropertiesToken = devProperties.getProperty('api.themoviedb')

ext.themoviedbToken = devPropertiesToken != null ? devPropertiesToken : System.getenv('api.themoviedb')

android {
    compileSdkVersion toolchainVersions.compileSdk
    buildToolsVersion toolchainVersions.buildTools
    defaultConfig {
        applicationId "com.vicidroid.amalia.sample"
        minSdkVersion toolchainVersions.minSdk
        targetSdkVersion toolchainVersions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField('String', 'themoviedbToken', themoviedbToken ?: "NOT_THE_REAL_KEY")

    }

    // Target Java 8 to use androidx.lifecycle.DefaultLifecycleObserver
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    repositories {
        maven { url 'https://jitpack.io' }
    }
}

androidExtensions {
    experimental = true
}

kapt {
    useBuildCache = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // JETBRAINS
    implementation deps.coroutines
    implementation deps.kotlinStdlib

    // ANDROID X
    implementation androidXDeps.appcompat
    implementation androidXDeps.annotation
    implementation androidXDeps.coreKtx
    implementation androidXDeps.constraintLayout
    implementation androidXDeps.legacySupport
    implementation androidXDeps.recyclerview
    implementation androidXDeps.savedState

    // ANDROID ARCHITECTURE COMPONENTS
    // Required to use androidx.lifecycle.DefaultLifecycleObserver
    implementation archDeps.commonJava8

    // MATERIAL
    implementation 'com.google.android.material:material:1.1.0-alpha07'

    // TESTS
    testImplementation deps.junit

    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.0'
    implementation "com.squareup.moshi:moshi:1.8.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"

    implementation('com.github.bumptech.glide:glide:4.9.0') {
        exclude group: "com.android.support"
    }

    // AMALIA LIB EXTERNAL
//    implementation 'com.github.vicidroiddev.amalia:amalia-core:0.3.6@aar'
//    implementation 'com.github.vicidroiddev.amalia:amalia-coroutine-scopes:0.3.6@aar'

    // AMALIA LIB INTERNAL
    implementation project(path: ':amalia-core')
    implementation project(path: ':amalia-coroutine-scopes')

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-alpha-3'
}

apply plugin: 'kotlin-android-extensions'
